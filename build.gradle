plugins {
    id 'java-platform'
    id 'maven-publish'
}

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform("org.springframework.boot:spring-boot-dependencies:3.3.1")
    api platform("org.springframework.cloud:spring-cloud-dependencies:2023.0.2")

    constraints {
        api "com.github.ahunigel:spring-toolkit:3.3.10"
        api "com.github.ahunigel:spring-test-toolkit:3.3.10"
        api "com.github.ahunigel:spring-security-oauth2-test:3.3.11"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ahunigel/${project.name}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.javaPlatform

            pom {
                name = project.name
                description = 'springkit maven BOMs'
                url = "https://github.com/ahunigel/${project.name}"
                developers {
                    developer {
                        id = 'ahunigel'
                        name = 'Nigel Zheng'
                        email = 'ahunigel@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:ahunigel/${project.name}.git"
                    url = "https://github.com/ahunigel/${project.name}"
                }
            }
        }
    }
}
